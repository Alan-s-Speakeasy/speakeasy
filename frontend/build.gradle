buildscript {
    repositories {
        mavenCentral()
    }
}
plugins {
    /// Plugin to handle NodeJS
    id 'base'
    id 'com.github.node-gradle.node' version '3.0.1'
}

configurations {
    frontendFiles {
        canBeConsumed = true
        canBeResolved = false
    }
}

node {
    // Updated to 16.10 for this version of angular
    version = '16.10.0'
    download = true
    workDir = file("${project.projectDir}/.gradle/nodejs")
    npmWorkDir = file("${project.projectDir}/.gradle/npm")
    nodeProjectDir = file("${project.projectDir}")
}

task buildFrontend(type: NpmTask) {
    outputs.upToDateWhen {
        return file("$buildDir/dist").isDirectory()
    }
    args = ['run', 'pbuild']
    dependsOn npmInstall
}

task packageFrontend(type: Jar) {
  outputs.upToDateWhen {
    return file("$buildDir/lib/frontend.jar").exists()
  }
  dependsOn buildFrontend
  archiveBaseName.set('frontend')
  archiveExtension.set('jar')
  destinationDirectory.set(file("$buildDir/lib"))
  from("$buildDir/dist") {
    into 'html'
  }
}

artifacts {
    frontendFiles(packageFrontend.archiveFile) {
        builtBy packageFrontend
        type "jar"
    }
}
